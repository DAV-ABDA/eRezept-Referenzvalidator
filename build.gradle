plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0' apply false
    id "nebula.release" version "16.0.0"
}

nebulaRelease {
    releaseBranchPatterns = ['main']
}
tasks.prepare.dependsOn(":core:test", "cli:shadowJar")
tasks.snapshot.dependsOn(":packages:publish", ":core:publish", ":bom:publish")
tasks.final.dependsOn(":packages:publish", ":core:publish", ":bom:publish")

String hapiFhirVersion = "5.7.9"
allprojects{
    group = "de.abda"
}

boolean shouldSign(){
    gradle.taskGraph.hasTask("publish")
}
subprojects{
    apply{
        plugin "maven-publish"
        plugin "signing"
    }
    repositories {
        mavenLocal()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "https://repo.maven.apache.org/maven2/"
        }
    }
}
configure(subprojects.findAll {it.name == "core" || it.name == "cli"}) {
    apply{
        plugin "java-library"
    }
    dependencies{
        implementation("org.slf4j:slf4j-api:1.7.32")
        implementation("org.apache.commons:commons-text:1.10.0") // überschreiben HAPI dependencies -> Sicherheitslücke CVE-2022-42889
        implementation("ca.uhn.hapi.fhir:hapi-fhir-validation:$hapiFhirVersion") {
            exclude group:"ca.uhn.hapi.fhir", module:"hapi-fhir-jpaserver-model"
        }
        implementation("ca.uhn.hapi.fhir:hapi-fhir-structures-r4:$hapiFhirVersion")
        implementation("ca.uhn.hapi.fhir:hapi-fhir-validation-resources-r4:$hapiFhirVersion")
        implementation("com.fasterxml.woodstox:woodstox-core:6.2.7")
        implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.3")
        testImplementation("org.junit.jupiter:junit-jupiter:5.7.2")
    }
    java{
        sourceCompatibility = JavaVersion.VERSION_1_8
        withJavadocJar()
        withSourcesJar()
    }
    test {
        useJUnitPlatform()
        maxHeapSize("2G")
        systemProperty "user.language","en"
    }
}

def customPom(String artifactId, String descriptionText) {
    return {
        name = artifactId
        description = descriptionText;
        url = "https://github.com/DAV-ABDA/eRezept-Referenzvalidator"
        scm {
            connection = 'scm:git:git://github.com/DAV-ABDA/eRezept-Referenzvalidator'
            developerConnection = 'scm:git:ssh://github.com/DAV-ABDA/eRezept-Referenzvalidator'
            url = 'https://github.com/DAV-ABDA/eRezept-Referenzvalidator'
        }
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id = 'ABDA-FHIR-Team'
                name = 'ABDA FHIR Team'
                email = 'fhir@abda.de'
            }
        }
    }
}

def mavenRepo = {
    url(version.toString().endsWith('SNAPSHOT') ? "https://s01.oss.sonatype.org/content/repositories/snapshots/" : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
    credentials {
        username = project.hasProperty("sonatypeUsername") ? sonatypeUsername : ""
        password = project.hasProperty("sonatypePassword") ? sonatypePassword : ""
    }
}
project(":packages"){
    apply{
        plugin "java-library"
    }
    java{
        sourceCompatibility = JavaVersion.VERSION_1_8
        withJavadocJar()
        withSourcesJar()
    }
    publishing {
        publications {
            maven(MavenPublication) {
                artifactId='fhir-validator-packages'
                from components.java
                pom customPom(artifactId, "The FHIR Packages for the ABDA ePrescription FHIR reference validator")
            }
        }
        repositories{
            maven mavenRepo
        }
    }
    signing {
        required {shouldSign()}
        sign publishing.publications.maven
    }
}

project(":core"){
    dependencies{
        api("ca.uhn.hapi.fhir:hapi-fhir-base:$hapiFhirVersion")
        implementation(project(":packages"))
        testRuntimeOnly(project(":packages"))
        testImplementation 'org.slf4j:slf4j-log4j12:1.7.32'
    }

    publishing {
        publications {
            maven(MavenPublication) {
                artifactId='fhir-validator-core'
                from (components.java)
                pom customPom(artifactId, "The ABDA ePrescription FHIR reference validator")
            }
        }

        repositories{
            maven mavenRepo
        }
    }
    signing {
        required {shouldSign()}
        sign publishing.publications.maven
    }
}

project(":bom"){
    apply {
        plugin 'java-platform'
        plugin "maven-publish"
        plugin "signing"
    }
    javaPlatform {
        allowDependencies()
    }
    dependencies{
        constraints{
            runtime("ca.uhn.hapi.fhir:hapi-fhir-validation:$hapiFhirVersion")
            runtime("ca.uhn.hapi.fhir:hapi-fhir-structures-r4:$hapiFhirVersion")
            runtime("ca.uhn.hapi.fhir:hapi-fhir-validation-resources-r4:$hapiFhirVersion")
            runtime(project(":packages"))
            runtime(project(":core"))
        }
    }
    publishing {
        publications {
            maven(MavenPublication) {
                artifactId='fhir-validator-bom'
                from components.javaPlatform
                pom customPom(artifactId, "The ABDA ePrescription FHIR reference validator BOM")
            }
        }
        repositories{
            maven mavenRepo
        }
    }
    signing {
        required {shouldSign()}
        sign publishing.publications.maven
    }
}



